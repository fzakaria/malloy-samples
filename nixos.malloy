/**
 * Create a BigQuery table that is the result of:
 *   SELECT
 *   *,
 *   ARRAY_LENGTH(parent) > 1 AS is_merge_commit
 *   FROM `bigquery-public-data.github_repos.commits` commits
 *   WHERE 'NixOS/nixpkgs' IN UNNEST(repo_name) AND
 *         EXTRACT(YEAR FROM TIMESTAMP_SECONDS(author.time_sec)) >= 2019
 *
 * This is to save on some cost to avoid scanning the whole table.
 */
sql: t is {
    select: """ SELECT * FROM `google.com:fmzakari-looker.malloydev.github_nixpkgs_commits_from_2019` """
    connection: "bigquery" 
}

source: nixpkgs_commits is from_sql(t) {
    where: 
        // We do not want merge commits since that will double count the commiter
        // as the one who merged it.
        // This should only keep the PullRequest commit now.
        not is_merge_commit        
    dimension:
        author_time is timestamp_seconds(author.time_sec)
        package is regexp_extract(subject, r'^([\w-]+):.+')
       
}

query: author_package is nixpkgs_commits -> {
    where: package != null
    group_by: author_name is author.name
    nest: by_package is {
        group_by: package
        aggregate: commit_count is count(distinct commit)
    }
}


query: package_author is nixpkgs_commits -> {
    where: package != null
    group_by: package
    nest: by_author is {
        group_by: author_name is author.name
        aggregate: commit_count is count(distinct commit)
    }
}

query: package_time is nixpkgs_commits -> {
    where: package != null
    group_by: package
    nest: by_time is {
        group_by: t is author_time.month
        aggregate: c is count(distinct commit)
    }
}

// source: file_commits is from(nixpkgs_commits -> {
//     group_by: difference.old_path, author_time_month is author_time.month
//     aggregate: commit_count is count(distinct commit)
// })

query: top10_commiters_dashboard is nixpkgs_commits -> {
    //TODO(fmzakari): this join_many shouldn't need to be here. File bug?
    join_one: package_author is from(->package_author) on package_author.package = package,
    join_one: package_time is from(->package_time) with package
    
    group_by: author.name
    aggregate: commit_count is count(distinct commit)
    top: 10

    nest: commits_by_time_line_chart is {
        group_by: author_time_month is author_time.month
        aggregate: commit_count is count(distinct commit)
    }

    nest: top_10_files is {
        group_by: difference.old_path
        aggregate: change_count is count(distinct commit)
        top: 10
        // nest: commits_by_time_line_chart is {
        //     group_by: author_time_month is author_time.month
        //     aggregate: commit_count is count(distinct commit)
        // }
        nest: commits_by_time_line_chart is {
            group_by: t is  package_time.by_time.t
            aggregate: c is  package_time.by_time.c.sum()
        }
    }

    nest: top_10_packages is {
        where: package != null
        group_by: package
        aggregate: change_count is count(distinct commit)
        top: 10

        nest: top_3_commiters is {
          where: author.name != package_author.by_author.author_name
          group_by: package_author.by_author.author_name
          aggregate: change_count is package_author.by_author.commit_count.sum()
          top: 3
        }
    }

}